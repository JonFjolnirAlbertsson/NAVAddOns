OBJECT Codeunit 53102 Two Factor Authentication Mgt.
{
  OBJECT-PROPERTIES
  {
    Date=28-01-16;
    Time=21:59:25;
    Modified=Yes;
    Version List=SMS1.01;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {

    [EventSubscriber(Codeunit,1,OnBeforeCompanyOpen)]
    LOCAL PROCEDURE TwoFactorAuthenticationBeforeCompanyOpen@1();
    VAR
      UserSetup@1000 : Record 91;
      SMSCode@1001 : Text;
      MessageText@1004 : Text;
      UserResponse@1003 : Text;
      EnterSMSCode@1002 : Page 53102;
      Counter@1005 : Integer;
      CodeIsValid@1006 : Boolean;
      TryAgain@1007 : Boolean;
      SMSWebService@1008 : Codeunit 53101;
    BEGIN
      IF NOT UserSetup.GET(USERID) THEN
        EXIT;

      IF NOT UserSetup."Use Two Factor Authentication" THEN
        EXIT;

      SMSCode := FORMAT(RANDOM(100000000));
      MessageText := STRSUBSTNO('Use this code to login in %1: %2',COMPANYNAME,SMSCode);
      SMSWebService.SendSMS(UserSetup."Phone No.",MessageText);
      TryAgain := TRUE;

      WHILE TryAgain DO BEGIN
        CLEAR(EnterSMSCode);
        IF EnterSMSCode.RUNMODAL <> ACTION::OK THEN
          ERROR('You canceled the login procedure');

        UserResponse := EnterSMSCode.GetSMSCode;

        CodeIsValid := UserResponse = SMSCode;
        Counter += 1;
        TryAgain := (NOT CodeIsValid) AND (Counter < 3);
      END;

      IF NOT CodeIsValid THEN
        ERROR('You entered an invalid code for 3 times');
    END;

    BEGIN
    END.
  }
}

