OBJECT Table 99000758 Machine Center
{
  OBJECT-PROPERTIES
  {
    Date=20.04.17;
    Time=16.20.12;
    Modified=Yes;
    Version List=NAVW110.00,OPC1.0;
  }
  PROPERTIES
  {
    DataCaptionFields=No.,Name;
    OnInsert=BEGIN
               MfgSetup.GET;
               IF "No." = '' THEN BEGIN
                 MfgSetup.TESTFIELD("Machine Center Nos.");
                 NoSeriesMgt.InitSeries(MfgSetup."Machine Center Nos.",xRec."No. Series",0D,"No.","No. Series");
               END;
             END;

    OnModify=BEGIN
               "Last Date Modified" := TODAY;
             END;

    OnDelete=VAR
               ProdOrderRtngLine@1000 : Record 5409;
               StdCostWksh@1001 : Record 5841;
               CapLedgEntry@1002 : Record 5832;
             BEGIN
               CapLedgEntry.SETRANGE(Type,CapLedgEntry.Type::"Machine Center");
               CapLedgEntry.SETRANGE("No.","No.");
               IF NOT CapLedgEntry.ISEMPTY THEN
                 ERROR(Text007,TABLECAPTION,"No.",CapLedgEntry.TABLECAPTION);

               StdCostWksh.RESET;
               StdCostWksh.SETRANGE(Type,StdCostWksh.Type::"Machine Center");
               StdCostWksh.SETRANGE("No.","No.");
               IF NOT StdCostWksh.ISEMPTY THEN
                 ERROR(Text007,TABLECAPTION,"No.",StdCostWksh.TABLECAPTION);

               CalendarEntry.SETRANGE("Capacity Type",CalendarEntry."Capacity Type"::"Machine Center");
               CalendarEntry.SETRANGE("No.","No.");
               CalendarEntry.DELETEALL;

               CalAbsentEntry.SETRANGE("Capacity Type",CalendarEntry."Capacity Type"::"Machine Center");
               CalAbsentEntry.SETRANGE("No.","No.");
               CalAbsentEntry.DELETEALL;

               MfgCommentLine.SETRANGE("Table Name",MfgCommentLine."Table Name"::"Machine Center");
               MfgCommentLine.SETRANGE("No.","No.");
               MfgCommentLine.DELETEALL;

               ProdOrderRtngLine.SETRANGE(Type,ProdOrderRtngLine.Type::"Machine Center");
               ProdOrderRtngLine.SETRANGE("No.","No.");
               IF NOT ProdOrderRtngLine.ISEMPTY THEN
                 ERROR(Text000);
             END;

    OnRename=BEGIN
               "Last Date Modified" := TODAY;
             END;

    CaptionML=[ENU=Machine Center;
               NOR=Maskinsenter];
    LookupPageID=Page99000761;
    DrillDownPageID=Page99000761;
  }
  FIELDS
  {
    { 1   ;   ;No.                 ;Code20        ;AltSearchField=Search Name;
                                                   CaptionML=[ENU=No.;
                                                              NOR=Nr.] }
    { 3   ;   ;Name                ;Text50        ;OnValidate=BEGIN
                                                                "Search Name" := Name;
                                                              END;

                                                   CaptionML=[ENU=Name;
                                                              NOR=Navn] }
    { 4   ;   ;Search Name         ;Code50        ;CaptionML=[ENU=Search Name;
                                                              NOR=S›kenavn] }
    { 5   ;   ;Name 2              ;Text50        ;CaptionML=[ENU=Name 2;
                                                              NOR=Navn 2] }
    { 6   ;   ;Address             ;Text50        ;CaptionML=[ENU=Address;
                                                              NOR=Adresse] }
    { 7   ;   ;Address 2           ;Text50        ;CaptionML=[ENU=Address 2;
                                                              NOR=Adresse 2] }
    { 8   ;   ;City                ;Text30        ;TableRelation=IF (Country/Region Code=CONST()) "Post Code".City
                                                                 ELSE IF (Country/Region Code=FILTER(<>'')) "Post Code".City WHERE (Country/Region Code=FIELD(Country/Region Code));
                                                   OnValidate=BEGIN
                                                                PostCode.ValidateCity(City,"Post Code",County,"Country/Region Code",(CurrFieldNo <> 0) AND GUIALLOWED);
                                                              END;

                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=[ENU=City;
                                                              NOR=Sted] }
    { 9   ;   ;Post Code           ;Code20        ;TableRelation=IF (Country/Region Code=CONST()) "Post Code"
                                                                 ELSE IF (Country/Region Code=FILTER(<>'')) "Post Code" WHERE (Country/Region Code=FIELD(Country/Region Code));
                                                   OnValidate=BEGIN
                                                                PostCode.ValidatePostCode(City,"Post Code",County,"Country/Region Code",(CurrFieldNo <> 0) AND GUIALLOWED);
                                                              END;

                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=[ENU=Post Code;
                                                              NOR=Postnr.] }
    { 14  ;   ;Work Center No.     ;Code20        ;TableRelation="Work Center";
                                                   OnValidate=VAR
                                                                WorkCenter@1000 : Record 99000754;
                                                                ProdOrderRtngLine@1001 : Record 5409;
                                                                ProdOrderCapNeed@1002 : Record 5410;
                                                                PlanningRtngLine@1003 : Record 99000830;
                                                              BEGIN
                                                                IF "Work Center No." = xRec."Work Center No." THEN
                                                                  EXIT;

                                                                IF "Work Center No." <> '' THEN BEGIN
                                                                  WorkCenter.GET("Work Center No.");
                                                                  WorkCenter.TESTFIELD("Unit of Measure Code");
                                                                  "Queue Time Unit of Meas. Code" := WorkCenter."Queue Time Unit of Meas. Code";
                                                                  "Setup Time Unit of Meas. Code" := WorkCenter."Unit of Measure Code";
                                                                  "Wait Time Unit of Meas. Code" := WorkCenter."Unit of Measure Code";
                                                                  "Move Time Unit of Meas. Code" := WorkCenter."Unit of Measure Code";
                                                                END;
                                                                VALIDATE("Location Code",WorkCenter."Location Code");

                                                                CalendarEntry.SETCURRENTKEY("Capacity Type","No.");
                                                                CalendarEntry.SETRANGE("Capacity Type",CalendarEntry."Capacity Type"::"Machine Center");
                                                                CalendarEntry.SETRANGE("No.","No.");
                                                                IF NOT CalendarEntry.FIND('-') THEN
                                                                  EXIT;

                                                                IF CurrFieldNo <> 0 THEN
                                                                  IF NOT CONFIRM(Text001,FALSE,FIELDCAPTION("Work Center No."))
                                                                  THEN BEGIN
                                                                    "Work Center No." := xRec."Work Center No.";
                                                                    EXIT;
                                                                  END;

                                                                Window.OPEN(
                                                                  Text002 +
                                                                  Text003 +
                                                                  Text004 +
                                                                  Text006);

                                                                i := 0;
                                                                NoOfRecords := CalendarEntry.COUNT;
                                                                IF CalendarEntry.FIND('-') THEN
                                                                  REPEAT
                                                                    i := i + 1;
                                                                    Window.UPDATE(1,i);
                                                                    Window.UPDATE(2,ROUND(i / NoOfRecords * 10000,1));
                                                                    CalendarEntry.VALIDATE("Work Center No.","Work Center No.");
                                                                    CalendarEntry.MODIFY;
                                                                  UNTIL CalendarEntry.NEXT = 0;

                                                                i := 0;
                                                                CalAbsentEntry.SETCURRENTKEY("Capacity Type","No.");
                                                                CalAbsentEntry.SETRANGE("Capacity Type",CalendarEntry."Capacity Type"::"Machine Center");
                                                                CalAbsentEntry.SETRANGE("No.","No.");
                                                                NoOfRecords := CalAbsentEntry.COUNT;
                                                                IF CalAbsentEntry.FIND('-') THEN
                                                                  REPEAT
                                                                    i := i + 1;
                                                                    Window.UPDATE(3,i);
                                                                    Window.UPDATE(4,ROUND(i / NoOfRecords * 10000,1));
                                                                    CalAbsentEntry.VALIDATE("Work Center No.","Work Center No.");
                                                                    CalAbsentEntry.MODIFY;
                                                                  UNTIL CalAbsentEntry.NEXT = 0;

                                                                i := 0;
                                                                ProdOrderCapNeed.SETCURRENTKEY(Type,"No.");
                                                                ProdOrderCapNeed.SETRANGE(Type,ProdOrderCapNeed.Type::"Machine Center");
                                                                ProdOrderCapNeed.SETRANGE("No.","No.");
                                                                NoOfRecords := ProdOrderCapNeed.COUNT;
                                                                IF ProdOrderCapNeed.FIND('-') THEN
                                                                  REPEAT
                                                                    i := i + 1;
                                                                    Window.UPDATE(7,i);
                                                                    Window.UPDATE(8,ROUND(i / NoOfRecords * 10000,1));
                                                                    ProdOrderCapNeed.VALIDATE("Work Center No.","Work Center No.");
                                                                    ProdOrderCapNeed.MODIFY;
                                                                  UNTIL ProdOrderCapNeed.NEXT = 0;

                                                                MODIFY;

                                                                RtngLine.SETCURRENTKEY(Type,"No.");
                                                                RtngLine.SETRANGE(Type,RtngLine.Type::"Machine Center");
                                                                RtngLine.SETRANGE("No.","No.");
                                                                IF RtngLine.FIND('-') THEN
                                                                  REPEAT
                                                                    RtngLine.VALIDATE("Work Center No.","Work Center No.");
                                                                    RtngLine.MODIFY;
                                                                  UNTIL RtngLine.NEXT = 0;

                                                                PlanningRtngLine.SETCURRENTKEY(Type,"No.");
                                                                PlanningRtngLine.SETRANGE(Type,PlanningRtngLine.Type::"Machine Center");
                                                                PlanningRtngLine.SETRANGE("No.","No.");
                                                                IF PlanningRtngLine.FIND('-') THEN
                                                                  REPEAT
                                                                    PlanningRtngLine.VALIDATE("Work Center No.","Work Center No.");
                                                                    PlanningRtngLine.MODIFY;
                                                                  UNTIL PlanningRtngLine.NEXT = 0;

                                                                ProdOrderRtngLine.SETCURRENTKEY(Type,"No.");
                                                                ProdOrderRtngLine.SETRANGE(Type,PlanningRtngLine.Type::"Machine Center");
                                                                ProdOrderRtngLine.SETRANGE("No.","No.");
                                                                IF ProdOrderRtngLine.FIND('-') THEN
                                                                  REPEAT
                                                                    ProdOrderRtngLine.VALIDATE("Work Center No.","Work Center No.");
                                                                    ProdOrderRtngLine.MODIFY;
                                                                  UNTIL ProdOrderRtngLine.NEXT = 0;

                                                                Window.CLOSE;
                                                              END;

                                                   CaptionML=[ENU=Work Center No.;
                                                              NOR=Arbeidssenternr.] }
    { 19  ;   ;Direct Unit Cost    ;Decimal       ;OnValidate=BEGIN
                                                                VALIDATE("Indirect Cost %");
                                                              END;

                                                   CaptionML=[ENU=Direct Unit Cost;
                                                              NOR=Direkte enhetskost];
                                                   MinValue=0;
                                                   AutoFormatType=2 }
    { 20  ;   ;Indirect Cost %     ;Decimal       ;OnValidate=BEGIN
                                                                GetGLSetup;
                                                                "Unit Cost" :=
                                                                  ROUND(
                                                                    "Direct Unit Cost" * (1 + "Indirect Cost %" / 100) + "Overhead Rate",
                                                                    GLSetup."Unit-Amount Rounding Precision");
                                                              END;

                                                   CaptionML=[ENU=Indirect Cost %;
                                                              NOR=Indirekte kost-%];
                                                   DecimalPlaces=0:5;
                                                   MinValue=0 }
    { 21  ;   ;Unit Cost           ;Decimal       ;OnValidate=BEGIN
                                                                GetGLSetup;
                                                                "Direct Unit Cost" :=
                                                                  ROUND(("Unit Cost" - "Overhead Rate") / (1 + "Indirect Cost %" / 100),
                                                                    GLSetup."Unit-Amount Rounding Precision");
                                                              END;

                                                   CaptionML=[ENU=Unit Cost;
                                                              NOR=Enhetskost];
                                                   DecimalPlaces=2:5;
                                                   MinValue=0;
                                                   AutoFormatType=2 }
    { 22  ;   ;Queue Time          ;Decimal       ;CaptionML=[ENU=Queue Time;
                                                              NOR=K›tid];
                                                   DecimalPlaces=0:5;
                                                   MinValue=0 }
    { 23  ;   ;Queue Time Unit of Meas. Code;Code10;
                                                   TableRelation="Capacity Unit of Measure";
                                                   CaptionML=[ENU=Queue Time Unit of Meas. Code;
                                                              NOR=K›tidsenhetskode] }
    { 26  ;   ;Last Date Modified  ;Date          ;CaptionML=[ENU=Last Date Modified;
                                                              NOR=Endret den];
                                                   Editable=No }
    { 27  ;   ;Comment             ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("Manufacturing Comment Line" WHERE (Table Name=CONST(Machine Center),
                                                                                                         No.=FIELD(No.)));
                                                   CaptionML=[ENU=Comment;
                                                              NOR=Merknad];
                                                   Editable=No }
    { 31  ;   ;Capacity            ;Decimal       ;CaptionML=[ENU=Capacity;
                                                              NOR=Kapasitet];
                                                   DecimalPlaces=0:5;
                                                   MinValue=0 }
    { 32  ;   ;Efficiency          ;Decimal       ;InitValue=100;
                                                   CaptionML=[ENU=Efficiency;
                                                              NOR=Effektivitet];
                                                   DecimalPlaces=0:5;
                                                   MinValue=0 }
    { 33  ;   ;Maximum Efficiency  ;Decimal       ;CaptionML=[ENU=Maximum Efficiency;
                                                              NOR=Maks. effektivitet];
                                                   DecimalPlaces=0:5;
                                                   MinValue=0 }
    { 34  ;   ;Minimum Efficiency  ;Decimal       ;CaptionML=[ENU=Minimum Efficiency;
                                                              NOR=Min. effektivitet];
                                                   DecimalPlaces=0:5;
                                                   MinValue=0 }
    { 38  ;   ;Blocked             ;Boolean       ;CaptionML=[ENU=Blocked;
                                                              NOR=Sperret] }
    { 39  ;   ;Date Filter         ;Date          ;FieldClass=FlowFilter;
                                                   CaptionML=[ENU=Date Filter;
                                                              NOR=Datofilter] }
    { 40  ;   ;Work Shift Filter   ;Code10        ;FieldClass=FlowFilter;
                                                   TableRelation="Work Shift";
                                                   CaptionML=[ENU=Work Shift Filter;
                                                              NOR=Arbeidsskiftfilter] }
    { 41  ;   ;Capacity (Total)    ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Calendar Entry"."Capacity (Total)" WHERE (Capacity Type=CONST(Machine Center),
                                                                                                              No.=FIELD(No.),
                                                                                                              Work Shift Code=FIELD(Work Shift Filter),
                                                                                                              Date=FIELD(Date Filter)));
                                                   CaptionML=[ENU=Capacity (Total);
                                                              NOR=Kapasitet (total)];
                                                   DecimalPlaces=0:5;
                                                   Editable=No }
    { 42  ;   ;Capacity (Effective);Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Calendar Entry"."Capacity (Effective)" WHERE (Capacity Type=CONST(Machine Center),
                                                                                                                  No.=FIELD(No.),
                                                                                                                  Work Shift Code=FIELD(Work Shift Filter),
                                                                                                                  Date=FIELD(Date Filter)));
                                                   CaptionML=[ENU=Capacity (Effective);
                                                              NOR=Kapasitet (effektiv)];
                                                   DecimalPlaces=0:5;
                                                   Editable=No }
    { 44  ;   ;Prod. Order Need (Qty.);Decimal    ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Prod. Order Capacity Need"."Allocated Time" WHERE (Type=CONST(Machine Center),
                                                                                                                       No.=FIELD(No.),
                                                                                                                       Status=FIELD(Prod. Order Status Filter),
                                                                                                                       Date=FIELD(Date Filter),
                                                                                                                       Requested Only=CONST(No)));
                                                   CaptionML=[ENU=Prod. Order Need (Qty.);
                                                              NOR=Prod.ordrebehov (ant.)];
                                                   DecimalPlaces=0:5;
                                                   Editable=No }
    { 45  ;   ;Prod. Order Need Amount;Decimal    ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Prod. Order Routing Line"."Expected Operation Cost Amt." WHERE (Type=CONST(Machine Center),
                                                                                                                                    No.=FIELD(No.),
                                                                                                                                    Status=FIELD(Prod. Order Status Filter)));
                                                   CaptionML=[ENU=Prod. Order Need Amount;
                                                              NOR=Prod.ordrebehov - bel›p];
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 47  ;   ;Prod. Order Status Filter;Option   ;FieldClass=FlowFilter;
                                                   CaptionML=[ENU=Prod. Order Status Filter;
                                                              NOR=Prod.ordrestatusfilter];
                                                   OptionCaptionML=[ENU=Simulated,Planned,Firm Planned,Released,Finished;
                                                                    NOR=Simulert,Planlagt,Fast planlagt,Frigitt,Ferdig];
                                                   OptionString=Simulated,Planned,Firm Planned,Released,Finished }
    { 50  ;   ;Setup Time          ;Decimal       ;CaptionML=[ENU=Setup Time;
                                                              NOR=Oppstillingstid];
                                                   DecimalPlaces=0:5;
                                                   MinValue=0 }
    { 52  ;   ;Wait Time           ;Decimal       ;CaptionML=[ENU=Wait Time;
                                                              NOR=Ventetid];
                                                   DecimalPlaces=0:5;
                                                   MinValue=0 }
    { 53  ;   ;Move Time           ;Decimal       ;CaptionML=[ENU=Move Time;
                                                              NOR=Transporttid];
                                                   DecimalPlaces=0:5;
                                                   MinValue=0 }
    { 54  ;   ;Fixed Scrap Quantity;Decimal       ;CaptionML=[ENU=Fixed Scrap Quantity;
                                                              NOR=Fast vrakantall];
                                                   DecimalPlaces=0:5;
                                                   MinValue=0 }
    { 55  ;   ;Scrap %             ;Decimal       ;CaptionML=[ENU=Scrap %;
                                                              NOR=Vrak-%];
                                                   DecimalPlaces=0:5;
                                                   MinValue=0 }
    { 56  ;   ;Setup Time Unit of Meas. Code;Code10;
                                                   TableRelation="Capacity Unit of Measure";
                                                   CaptionML=[ENU=Setup Time Unit of Meas. Code;
                                                              NOR=Oppstillingstid - enhetskode] }
    { 57  ;   ;Wait Time Unit of Meas. Code;Code10;TableRelation="Capacity Unit of Measure";
                                                   CaptionML=[ENU=Wait Time Unit of Meas. Code;
                                                              NOR=Ventetid - enhetskode] }
    { 58  ;   ;Send-Ahead Quantity ;Decimal       ;CaptionML=[ENU=Send-Ahead Quantity;
                                                              NOR=Overlappantall];
                                                   DecimalPlaces=0:5;
                                                   MinValue=0 }
    { 59  ;   ;Move Time Unit of Meas. Code;Code10;TableRelation="Capacity Unit of Measure";
                                                   CaptionML=[ENU=Move Time Unit of Meas. Code;
                                                              NOR=Transporttid - enhetskode] }
    { 60  ;   ;Flushing Method     ;Option        ;InitValue=Manual;
                                                   CaptionML=[ENU=Flushing Method;
                                                              NOR=Trekkmetode];
                                                   OptionCaptionML=[ENU=Manual,Forward,Backward;
                                                                    NOR=Manuell,Forlengs,Baklengs];
                                                   OptionString=Manual,Forward,Backward }
    { 62  ;   ;Minimum Process Time;Decimal       ;CaptionML=[ENU=Minimum Process Time;
                                                              NOR=Minimum behandlingstid];
                                                   DecimalPlaces=0:5;
                                                   MinValue=0 }
    { 63  ;   ;Maximum Process Time;Decimal       ;CaptionML=[ENU=Maximum Process Time;
                                                              NOR=Maksimal behandlingstid];
                                                   DecimalPlaces=0:5;
                                                   MinValue=0 }
    { 64  ;   ;Concurrent Capacities;Decimal      ;CaptionML=[ENU=Concurrent Capacities;
                                                              NOR=Samtidige kapasiteter];
                                                   DecimalPlaces=0:5;
                                                   MinValue=0 }
    { 65  ;   ;Item Filter         ;Code20        ;FieldClass=FlowFilter;
                                                   TableRelation=Item;
                                                   CaptionML=[ENU=Item Filter;
                                                              NOR=Varefilter] }
    { 66  ;   ;Stop Code Filter    ;Code10        ;FieldClass=FlowFilter;
                                                   TableRelation=Stop;
                                                   CaptionML=[ENU=Stop Code Filter;
                                                              NOR=Stoppkodefilter] }
    { 67  ;   ;Scrap Code Filter   ;Code10        ;FieldClass=FlowFilter;
                                                   TableRelation=Scrap;
                                                   CaptionML=[ENU=Scrap Code Filter;
                                                              NOR=Vrakkodefilter] }
    { 80  ;   ;No. Series          ;Code10        ;TableRelation="No. Series";
                                                   CaptionML=[ENU=No. Series;
                                                              NOR=Nr.serie];
                                                   Editable=No }
    { 81  ;   ;Overhead Rate       ;Decimal       ;OnValidate=BEGIN
                                                                VALIDATE("Indirect Cost %");
                                                              END;

                                                   CaptionML=[ENU=Overhead Rate;
                                                              NOR=Sats for indirekte kostnader];
                                                   AutoFormatType=2 }
    { 82  ;   ;Gen. Prod. Posting Group;Code10    ;TableRelation="Gen. Product Posting Group";
                                                   CaptionML=[ENU=Gen. Prod. Posting Group;
                                                              NOR=Bokf›ringsgruppe - vare] }
    { 83  ;   ;County              ;Text30        ;CaptionML=[ENU=County;
                                                              NOR=Fylke/delstat] }
    { 84  ;   ;Country/Region Code ;Code10        ;TableRelation=Country/Region;
                                                   CaptionML=[ENU=Country/Region Code;
                                                              NOR=Lands-/regionkode] }
    { 7300;   ;Location Code       ;Code10        ;TableRelation=Location.Code WHERE (Use As In-Transit=CONST(No),
                                                                                      Bin Mandatory=CONST(Yes));
                                                   OnValidate=VAR
                                                                WorkCenter@1000 : Record 99000754;
                                                                AutoUpdate@1001 : Boolean;
                                                              BEGIN
                                                                IF "Location Code" <> xRec."Location Code" THEN BEGIN
                                                                  IF ("Work Center No." = '') AND ("Location Code" <> '') THEN
                                                                    ERROR(Text008,FIELDCAPTION("Location Code"),TABLECAPTION,WorkCenter.TABLECAPTION);

                                                                  IF "Open Shop Floor Bin Code" <> '' THEN BEGIN
                                                                    IF ConfirmAutoRemovalOfBinCode(AutoUpdate) THEN
                                                                      VALIDATE("Open Shop Floor Bin Code",'')
                                                                    ELSE
                                                                      TESTFIELD("Open Shop Floor Bin Code",'');
                                                                  END;
                                                                  IF "To-Production Bin Code" <> '' THEN BEGIN
                                                                    IF ConfirmAutoRemovalOfBinCode(AutoUpdate) THEN
                                                                      VALIDATE("To-Production Bin Code",'')
                                                                    ELSE
                                                                      TESTFIELD("To-Production Bin Code",'');
                                                                  END;
                                                                  IF "From-Production Bin Code" <> '' THEN BEGIN
                                                                    IF ConfirmAutoRemovalOfBinCode(AutoUpdate) THEN
                                                                      VALIDATE("From-Production Bin Code",'')
                                                                    ELSE
                                                                      TESTFIELD("From-Production Bin Code",'');
                                                                  END;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Location Code;
                                                              NOR=Lokasjonskode];
                                                   Editable=No }
    { 7301;   ;Open Shop Floor Bin Code;Code20    ;TableRelation=Bin.Code WHERE (Location Code=FIELD(Location Code));
                                                   OnValidate=VAR
                                                                WhseIntegrationMgt@1000 : Codeunit 7317;
                                                              BEGIN
                                                                WhseIntegrationMgt.CheckBinCode("Location Code",
                                                                  "Open Shop Floor Bin Code",
                                                                  FIELDCAPTION("Open Shop Floor Bin Code"),
                                                                  DATABASE::"Machine Center","No.");
                                                              END;

                                                   CaptionML=[ENU=Open Shop Floor Bin Code;
                                                              NOR=pen prod.hyllekode] }
    { 7302;   ;To-Production Bin Code;Code20      ;TableRelation=Bin.Code WHERE (Location Code=FIELD(Location Code));
                                                   OnValidate=VAR
                                                                WhseIntegrationMgt@1000 : Codeunit 7317;
                                                              BEGIN
                                                                WhseIntegrationMgt.CheckBinCode("Location Code",
                                                                  "To-Production Bin Code",
                                                                  FIELDCAPTION("To-Production Bin Code"),
                                                                  DATABASE::"Machine Center","No.");
                                                              END;

                                                   CaptionML=[ENU=To-Production Bin Code;
                                                              NOR=Til-Hyllekode for produksjon] }
    { 7303;   ;From-Production Bin Code;Code20    ;TableRelation=Bin.Code WHERE (Location Code=FIELD(Location Code));
                                                   OnValidate=VAR
                                                                WhseIntegrationMgt@1000 : Codeunit 7317;
                                                              BEGIN
                                                                WhseIntegrationMgt.CheckBinCode("Location Code",
                                                                  "From-Production Bin Code",
                                                                  FIELDCAPTION("From-Production Bin Code"),
                                                                  DATABASE::"Machine Center","No.");
                                                              END;

                                                   CaptionML=[ENU=From-Production Bin Code;
                                                              NOR=Fra-Hyllekode for produksjon] }
    { 83001;  ;Show in OPC         ;Boolean       ;CaptionML=[ENU=Show in OPC;
                                                              NOR=Vises i OPC];
                                                   Description=OPC }
    { 83002;  ;Clocking            ;Boolean       ;CaptionML=[ENU=Clocking;
                                                              NOR=M›nstring];
                                                   Description=OPC }
    { 83003;  ;Employee No.        ;Code20        ;CaptionML=[ENU=Employee No. (H and L);
                                                              NOR=Ansattnr. (H og L)];
                                                   Description=OPC }
    { 83004;  ;Time registrations  ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Time Register".Quantity WHERE (Machine Center=FIELD(No.),
                                                                                                   Start Date=FIELD(Date Filter)));
                                                   Description=OPC }
    { 83005;  ;Earliest Time Reg.  ;Time          ;FieldClass=FlowField;
                                                   CalcFormula=Min("Time Register"."Start Time" WHERE (Machine Center=FIELD(No.),
                                                                                                       Start Date=FIELD(Date Filter)));
                                                   Description=OPC }
    { 83006;  ;Latest Time Reg.    ;Time          ;FieldClass=FlowField;
                                                   CalcFormula=Max("Time Register"."End Time" WHERE (Machine Center=FIELD(No.),
                                                                                                     Start Date=FIELD(Date Filter)));
                                                   Description=OPC }
    { 83007;  ;Deduct - Lunch      ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("Time Register" WHERE (Deduction Type=CONST(Lunch),
                                                                                            Start Date=FIELD(Date Filter),
                                                                                            Machine Center=FIELD(No.)));
                                                   Description=OPC }
    { 83008;  ;Invoiced Qty.       ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Capacity Ledger Entry"."Invoiced Quantity" WHERE (No.=FIELD(No.),
                                                                                                                      Type=FILTER(Machine Center),
                                                                                                                      Posting Date=FIELD(Date Filter)));
                                                   Description=OPC }
    { 83009;  ;Birth Date          ;Date          ;CaptionML=[ENU=Birth Date;
                                                              NOR=F›dselsdato];
                                                   Description=OPC }
    { 83010;  ;Allow OPC Move      ;Boolean       ;CaptionML=NOR=Tillat flytting av komponenter via OPC;
                                                   Description=OPC }
  }
  KEYS
  {
    {    ;No.                                     ;Clustered=Yes }
    {    ;Search Name                              }
    {    ;Work Center No.                          }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=The Machine Center is being used on production orders.;NOR=Maskinsenteret brukes i forbindelse med produksjonsordrer.';
      Text001@1001 : TextConst 'ENU=Do you want to change %1?;NOR=Vil du endre %1?';
      Text002@1002 : TextConst 'ENU=Work Center No. is corrected on\\;NOR=Arbeidssenternr. er korrigert\\';
      Text003@1003 : TextConst 'ENU=Calendar Entry    #1###### @2@@@@@@@@@@@@@\;NOR=Kalenderpost      #1###### @2@@@@@@@@@@@@@\';
      Text004@1004 : TextConst 'ENU=Calendar Absent.  #3###### @4@@@@@@@@@@@@@\;NOR=Kalenderfrav‘r    #3###### @4@@@@@@@@@@@@@\';
      Text006@1005 : TextConst 'ENU=Prod. Order Need  #7###### @8@@@@@@@@@@@@@;NOR=Prod.ordrebehov   #7###### @8@@@@@@@@@@@@@';
      PostCode@1020 : Record 225;
      MfgSetup@1006 : Record 99000765;
      MachineCenter@1007 : Record 99000758;
      CalendarEntry@1008 : Record 99000757;
      CalAbsentEntry@1009 : Record 99000760;
      MfgCommentLine@1010 : Record 99000770;
      RtngLine@1011 : Record 99000764;
      GLSetup@1017 : Record 98;
      NoSeriesMgt@1013 : Codeunit 396;
      Window@1014 : Dialog;
      i@1015 : Integer;
      NoOfRecords@1016 : Integer;
      GLSetupRead@1018 : Boolean;
      Text007@1019 : TextConst '@@@="%1 = Table caption; %2 = Field Value; %3 = Table Caption";ENU=You cannot delete %1 %2 because there is at least one %3 associated with it.;NOR=Du kan ikke slette %1 %2 fordi det finnes minst ‚n %3 tilknyttet.';
      Text008@1021 : TextConst 'ENU=You cannot change the %1 on %2 unless it is linked to a %3.;NOR=Du kan ikke endre %1 p† %2 med mindre koblet til %3.';
      Text009@1022 : TextConst 'ENU=If you change the %1, then all bin codes on the %2 will be removed. Are you sure that you want to continue?;NOR=Hvis du endrer %1, vil alle hyllekoder p† %2 bli fjernet. Er du sikker p† at du vil fortsette ?';

    PROCEDURE AssistEdit@2(OldMachineCenter@1000 : Record 99000758) : Boolean;
    BEGIN
      WITH MachineCenter DO BEGIN
        MachineCenter := Rec;
        MfgSetup.GET;
        MfgSetup.TESTFIELD("Machine Center Nos.");
        IF NoSeriesMgt.SelectSeries(MfgSetup."Machine Center Nos.",OldMachineCenter."No. Series","No. Series") THEN BEGIN
          NoSeriesMgt.SetSeries("No.");
          Rec := MachineCenter;
          EXIT(TRUE);
        END;
      END;
    END;

    LOCAL PROCEDURE GetGLSetup@14();
    BEGIN
      IF NOT GLSetupRead THEN
        GLSetup.GET;
      GLSetupRead := TRUE;
    END;

    LOCAL PROCEDURE ConfirmAutoRemovalOfBinCode@1(VAR AutoUpdate@1000 : Boolean) : Boolean;
    BEGIN
      IF AutoUpdate THEN
        EXIT(TRUE);

      IF CONFIRM(Text009,FALSE,FIELDCAPTION("Location Code"),TABLECAPTION) THEN
        AutoUpdate := TRUE;

      EXIT(AutoUpdate);
    END;

    PROCEDURE GetBinCode@9(UseFlushingMethod@1000 : Boolean;FlushingMethod@1001 : 'Manual,Forward,Backward,Pick + Forward,Pick + Backward') : Code[20];
    BEGIN
      IF NOT UseFlushingMethod THEN
        EXIT("From-Production Bin Code");

      CASE FlushingMethod OF
        FlushingMethod::Manual,
        FlushingMethod::"Pick + Forward",
        FlushingMethod::"Pick + Backward":
          EXIT("To-Production Bin Code");
        FlushingMethod::Forward,
        FlushingMethod::Backward:
          EXIT("Open Shop Floor Bin Code");
      END;
    END;

    BEGIN
    {
      +----------------------------------------------+
      OPC 5.01 (=5.0 SP1)
      ------------------------------------------------
      No  Date     ID  Description
      01. 04.11.09 HHE Opprettet feltene: 83001 "Show in OPC", 83470 Clocking, 83471 "Employee No."
      02. 21.01.10 BVM Nytt felt: Flowfelt Invoiced Qty, .
      03. 17.12.10 HHE Nytt felt: Birth date (›nsket av FRSK. Brukes i m›nstringstider->l›nnsgrunnlag)
      04. 04.03.10 HHE Nye  felt: Timeregistrations (brukes for summering av faktisk m›nstringstid)
                                  "Earilest start time" (tidspkt. for innstempling)
                                  "Latest end time" (tidspkt. for utstempling)
      05. 19.03.10 HHE Nytt felt "Deduct - Lunch" (for † se om det finnes fradrag-lunch for dagen)
    }
    END.
  }
}

